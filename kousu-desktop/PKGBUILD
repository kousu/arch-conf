pkgname=kousu-desktop
pkgdesc="Configuration management for kousu's workstations"
arch=('any')
url="https://github.com/kousu/arch-conf"

_pkgver() {
  [ -r "$startdir"/.pkgver ] && cat "$startdir"/.pkgver || (echo "r$(git rev-list --count HEAD).commit=$(git describe --always --dirty | sed s/-/+/g)")
}
pkgver="$(_pkgver)"
pkgrel=1

depends=(
  kousu-base

  iwd # wifi
  #macchanger # not needed if using iwd's AddressRandomization
  openconnect  # VPN

  alsa-utils

  bluez-utils  # also pulls in the rest of bluetooth # XXX do I need to add `systemctl enable --now bluetooth` somewhere?
  pipewire-pulse
  pipewire-alsa

  yubikey-manager

  # CLI
  pass # password manager
  pass-otp
  pwgen # alternate password generator

  # CLI :: Programming
  android-tools # adb! fastest way to get files in and out of a phone
  android-udev
  #jad
  #hollywood  # 'hacking'

  # decompiler
  #rizin
  #rz-ghidra
  #dotpeek

  # TUI
  ranger # curses file browser
  #lynx, or elinks, links, w3m # terminal browser

  # GUI
  #xorg-xinit # weirdly you can install X without xorg-xinit on Arch; I guess because lightdm/gdm/etc can serve its purpose?
  #xorg-server # weirdly, xorg-xinit doesn't pull this in; you'd think it would.
  #xorg-xhost
  xorg-xmodmap
  xorg-xkill
  xorg-xev
  xorg-fonts-100dpi
  xorg-fonts-75dpi
  noto-fonts-emoji # Google's Android emoji fonts, with the colourful faces and whatnot
  terminus-font
  otf-fira-sans

  # lightdm ?
  xclip
  #peek  # screen recorder (unmaintained. but it is sooo user friendly)
  vokoscreen # screen recorder

  ## GUI :: Hardware
  wireshark-qt

  transmission-qt
  #transmission-gtk # more traditional? But I'm trying out a KDE-based system...

  ## GUI :: Desktop Environment
  gvfs
  gvfs-smb
  gvfs-mtp

  ## GUI :: Editors
  #libreoffice-fresh # documents/spreadsheets/presentations/etc
  xournalpp   # PDF editor; mostly good for signing legal documents.
  #qpdf       # another PDF editor, less tools but more accurate
  obsidian    # Notebook
  # zettlr    # alternative notebook (obsidian is more popular and has lots of plugins)

  ## GUI :: Programming
  code  # vscode. sigh. it's the only good one
  ttf-fira-code
  # TODO: there's typst, rust, go and javascript plugins that need packages 

  ## GUI :: Web
  firefox
  firefox-decentraleyes
  firefox-extension-passff
  firefox-ublock-origin
  # XXX there's now also extra/ublock-origin?
  #chromium # very, very large

  ## GUI :: Comms
  dino
  gajim  # duplicates dino, but is both more annoying and more featureful so I keep it around
  signal-desktop

  ## GUI :: Media
  vlc # ?? or maybe i should just use whatever comes with KDE?
  vlc-plugin-ogg
  shortwave # radio-browser.info client

  ## Games
  #xboard
  #gnuchess  # "optional" for xboard, but not really: it's the AI to play against
  #openra
  #endless-sky
  #kgeography
)

optdepends=(
  'apostrophe: .md focus mode editor; BEWARE: pulls in pandoc which pulls in almost all of haskell'
)

package() {
  cd "${startdir}/src/"   # instead of prepare() { cp "${startdir}/src/." "${srcdir}" }

  # often PKGBUILDs use install(1) here, because it lets them control permissions
  # but install(1) can't do folders recursively; so instead, just cp -r, and set any permissions directly with chmod
  # that's safe enough: this is *building* a package for later deployment, not installing on a live system where a temporary gap in permissions is a risk
  cp -rp * "${pkgdir}"

  chmod 750 "${pkgdir}"/etc/sudoers.d
  chmod 600 "${pkgdir}"/etc/sudoers.d/*
}

# To control GTK/GNOME settings:
# - 'dconf' or 'gconf' (why are there two? who knows)
# To control Firefox settings:
# - Firefox defaults javascripts; plus extra Firefox extensions
# To control xsession settings:
# - if you're using sx, or xinit, or ...whazzit ~/.xsession
#
# In general, if you can't figure it out how to write a config file for a given app, you can do:
#   1. cp -r ~/.config A
#   1. make the change in the app's settings/GUI
#   2. cp -r ~/.config B
#   3. diff -ru A/ B/
# This should identify the change.
# Some apps don't use ~/.config, but they're getting rarer.
